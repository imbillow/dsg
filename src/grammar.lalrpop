use std::str::FromStr;
use crate::ast::{Expr,Opcode};

grammar;

Tier<Op, NextTier> = {
    Tier<Op, NextTier> Op NextTier => Box::new(Expr::Op(<>)),
    NextTier
}

pub Expr = Tier<ExprOp, Factor>;
pub Factor = Tier<FactorOp, Term>;


ExprOp: Opcode = { // (3)
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

FactorOp: Opcode = { // (3)
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};

pub Term: Box<Expr> = {
    Num => Box::new(Expr::Number(<>)),
    "(" <Expr> ")"
};

Num: i32 = <s:r"[0-9]+"> => i32::from_str(s).unwrap();
